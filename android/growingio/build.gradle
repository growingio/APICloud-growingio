apply plugin: 'com.android.library'

ext['growingio'] = ''

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.growingio.android:vds-android-agent:2.3.3@aar'
    provided files('../app/libs/apiEngine v1.1.0.jar')
}

afterEvaluate {
    configurations.compile.each {
        if (it.name.startsWith("vds-android-agent")){
            project.ext.growingio = it.absolutePath
            logger.info("found growingio aar path %s", ext.growingio)
        }
    }

    if (project.ext.growingio != null && project.ext.growingio.length() != 0){
        logger.info("create task zip apicloud")
        task copyArchiveForAPI(type: Copy){
            from "${project.ext.growingio}",
                    "$buildDir/outputs/aar/growingio-release.aar",
                    "${rootProject.rootDir}/app/src/main/assets/uzmap/module.json"
            into "$buildDir/tmp/growingioArchive"
        }

        task zipApiCloud(dependsOn: copyArchiveForAPI, type: Zip){
            archiveName 'GrowingIO.zip'
            destinationDir file("$buildDir/outputs/zip/")
            from "$buildDir/tmp/growingioArchive"
        }
    }
}